buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        classpath 'com.moowork.gradle:gradle-node-plugin:1.2.0'
    }
}

apply plugin: 'base'
apply plugin: 'com.moowork.node'

node {
    version = '12.18.3'
    yarnVersion = '1.22.10'
    download = true
}

yarn_run_build {
    inputs.files fileTree('public')
    inputs.files fileTree('src')

    inputs.file 'package.json'
    inputs.file 'yarn.lock'

    outputs.dir 'build'
}

task packageReactApp(type: Zip) {
    dependsOn yarn_run_build
    baseName 'react-app'
    extension 'jar'
    destinationDir file("${projectDir}/build_packageReactApp")
    from('build') {
        into 'static'
    }
}

configurations {
    yarnResources
}

configurations.default.extendsFrom(configurations.yarnResources)

artifacts {
    yarnResources(packageReactApp.archivePath) {
        builtBy packageReactApp
        type 'jar'
    }
}

assemble.dependsOn packageReactApp


clean {
    delete packageReactApp.archivePath
}
